fn CollectionView(@collection, @idx) {}

// Its only partially implemented right now because most use-cases
// don't require the complete collections api.
impl Collection for CollectionView = {
  fn len() = this.collection::len() - this.idx
  fn empty?() = this::len() == 0
  fn at(idx) = this.collection::at(this.idx + idx)
}
impl OrderedSequence for CollectionView = {
  fn first() = this.collection::at(this.idx)
  fn last() = this.collection::last()
}
impl :skip for CollectionView = fn(n) = CollectionView[this.collection, this.idx + n]

impl Symbol.iterator for CollectionView = fn*() {
  for i of this.idx..=this.collection::len() {
    yield this.collection[i]
  }
}

fn Lexer(@entries) {}

fn pass() {}
fn newline() {}

impl Call for Lexer = fn(str) {
  let tokens = []  
  let index = 0
  fn rest_of_string() = str.slice(index)

  fn scan() {
    let result = rest_of_string().match(this)
    if !result || result.index != 0 { return false }
    index = index + result[0].length
    return result[0]
  }

  let line = 1
  let col = 1
  while rest_of_string() != "" {
    let found = false  
    for [pattern type] of this.entries {
      if let value = pattern::scan() {
        if type == newline {
          line = line + 1
          col = 1
        } else if type != pass {
          tokens.push({type, value, line, col})
          col = col + value::len()
        } else {
          col = col + value::len()
        }
        found = true
        break
      }
    }
    if !found { raise!(Error["No token matched."]) }
  }

  return tokens
}

let lexer = Lexer{
  /^\n/ => newline
  /^\s+/ => pass
  /^\/\/.*/ => pass
  /^\,/ => pass
  /^\;/ => pass
  /^if\b/ => :if
  /^is\b/ => :is
  /^else\b/ => :else
  /^return\b/ => :return
  /^import\b/ => :import
  /^export\b/ => :export
  /^default\b/ => :default
  /^from\b/ => :from
  /^let\b/ => :let
  /^protocol\b/ => :protocol
  /^for\b/ => :for
  /^try\b/ => :try
  /^catch\b/ => :catch
  /^finally\b/ => :finally
  /^while\b/ => :while
  /^loop\b/ => :loop
  /^continue\b/ => :continue
  /^break\b/ => :break
  /^of\b/ => :of
  /^impl\b/ => :impl
  /^define\b/ => :define
  /^yield\b/ => :yield
  /^async\b/ => :async
  /^await\b/ => :await
  /^as\b/ => :as
  /^\=\>/ => :arrow
  /^\@/ => :at
  /^\&\&/ => :and_and
  /^\|\|/ => :or_or
  /^\?\?/ => :nullish
  /^\=\=\=/ => :triple_eq
  /^\!\=\=/ => :triple_not_eq
  /^\=\=/ => :double_eq
  /^\!\=/ => :not_eq
  /^\!/ => :bang
  /^\=/ => :eq
  /^fn\b/ => :fn
  /^\{/ => :open_b
  /^\}/ => :close_b
  /^\(/ => :open_p
  /^\)/ => :close_p
  /^[\-\+]?(\d*\.)?\d+[a-zA-Z]+/ => :custom_number_literal
  /^[\-\+]?(\d*\.)?\d+/ => :num
  /^\.\.\./ => :dot_dot_dot
  /^\.\./ => :dot_dot
  /^\./ => :dot
  /^\/.*\/[a-z]?/ => :regex_lit
  /^\>\=/ => :gt_eq
  /^\<\=/ => :lt_eq
  /^\>/ => :gt
  /^\</ => :lt
  /^\+/ => :plus
  /^\%/ => :mod
  /^\-/ => :minus
  /^\*\*/ => :pow
  /^\*/ => :times
  /^\&/ => :single_and
  /^\:\:/ => :double_colon
  /^\:[a-zA-Z_\?\!\$0-9\/\.]+/ => :keyword
  /^\:/ => :colon
  /^\// => :div
  /^\[/ => :open_sq
  /^\]/ => :close_sq
  /^\"([^\\\"]|\\.)*\"/s => :string_lit
  /^[a-zA-Z_\?\!\$0-9\>\-]+/ => :id
}

fn ParseError(expected_token_type, actual_token) {
  this.stack = Error[].stack
  this.message = str(
    "Expected: " expected_token_type
    " got " actual_token::at(:type)
    " @ " actual_token::at(:line)::as_str() ":" actual_token::at(:col)::as_str()
  )
}
ParseError.prototype = Error[]

fn expect_token!(kw) {
  if this::first()::at(:type) != kw {
    raise!(ParseError[kw, this::first()])
  } else {
    return this
  }
}

fn verify_exists!(parser) {
  if this::nil?() {
    raise!(Error["Parser Failed - Expected " + parser])
  } else {
    return this
  }
}

// PARSER MACHINE IMPL

protocol ParseInstruction

fn line_and_col({line, col}) = {line, col}

fn Init(@expr) {}
impl ParseInstruction for Init = fn([_expr, tokens]) =
  [{...this.expr, pos: line_and_col(tokens::first())} tokens]

fn One(@kw, @as) {}
impl ParseInstruction for One = fn([expr tokens]) {
  let {value, type} = tokens::expect_token!(this.kw)::first()
  return [expr::merge({[this.as]: value}) tokens.skip(1)]
}

fn Optional(@set_or_kw, @parse_fn, @as) {}
impl ParseInstruction for Optional = fn([expr tokens]) {  
  if tokens::empty?() { return [expr tokens] }
  // this is getting hairy, reimplementing ParseMap here..
  fn check_set(type) =
    ::any?(fn(val) {
      if val == type {
        return true
      } else if val.[Vector] {
        return val::has?(type)
      }
    })
  let {type} = tokens::first()
  if this.set_or_kw is Keyword && type == this.set_or_kw {
    return Then[this.parse_fn, this.as]::parse_step([expr tokens])
  } else if this.set_or_kw is Set && this.set_or_kw::check_set(type) {
    return Then[this.parse_fn, this.as]::parse_step([expr tokens])
  } else {
    return [expr tokens]
  }
}

impl ParseInstruction for Function = fn([_expr tokens]) = this(tokens)

fn Chomp(...kws) { this.kws = kws }
impl ParseInstruction for Chomp = fn([expr tokens]) {
  // this.kws::zip(tokens)::all!(fn([a b]) = a == b)
  // return [expr tokens::skip(this.kws::len())]
  let i = 0
  for kw of this.kws {
    tokens.skip(i)::expect_token!(kw)
    i = i + 1
  }
  return [expr, tokens.skip(i)]
}

fn Then(@parser, @kw) {}
impl ParseInstruction for Then = fn([expr tokens]) {
  let result = this.parser::call(tokens)
  if result::nil?() { return [expr tokens] }
  let [new_expr new_tokens] = result
  if this.kw {
    return [expr::merge({[this.kw]: new_expr}) new_tokens]
  } else {
    return [new_expr new_tokens]
  }
}

fn FMap(@f) {}
impl ParseInstruction for FMap = fn([expr tokens]) = [this.f::call(expr) tokens]

fn Until(@end_kw, @parser, @kw) {}
impl ParseInstruction for Until = fn([expr tokens]) {
  let exprs = []  
  while tokens::first()::at(:type) != this.end_kw {
    let [expr new_tokens] = this.parser::call(tokens)
      ::verify_exists!(this)
    exprs.push(expr)
    tokens = new_tokens
  }
  if this.kw {
    return [expr::merge({[this.kw]: exprs}) tokens]
  } else {
    return [exprs, tokens]
  }
}

fn Case(@parse_map, @kw) {}
impl ParseInstruction for Case = fn([expr tokens]) {
  if let [new_expr, new_tokens] = this.parse_map::call(tokens) {
    if this.kw {
      return [expr::merge({[this.kw]: new_expr}), new_tokens]
    } else {
      return [new_expr new_tokens]
    }
  } else {
    console.log(this.tokens::first(), this.parse_map)
    raise!(Error["Case Parse Failed"])
  }
}

fn Either(@set, @kw) {}
impl ParseInstruction for Either = fn([expr tokens]) {
  let op = this.set::call(tokens::first()::at(:type))::verify_exists!(this.set)
  let [new_expr, rest] = [tokens::first(), tokens.skip(1)]
  return [expr::merge({[this.kw]: new_expr::at(:value)}), rest]
}

fn parse_step(result) = this[ParseInstruction](result)

fn Parser(...instructions) { this.instructions = instructions }

impl Call for Parser = fn(tokens) =
  this::parse_step([null tokens])

fn AbortIf(@cond_fn) {}

impl ParseInstruction for Parser = fn(result) {
  for instruction of this.instructions {
    if instruction is AbortIf {
      if instruction.cond_fn::call(result) {
        return
      } else {
        continue
      }
    }
    result = instruction::parse_step(result)
  }
  return result
}

fn ParseMap(@entries) {}

impl Record for ParseMap = {
  fn keys() = this.entries::map(first)::into(Set[])
}

impl Call for ParseMap = fn(tokens, ...args) {
  if tokens::empty?() { return }

  for [pattern parser] of this.entries {
    if pattern == _ {
      return parser::call(tokens, ...args)
    }
    if pattern is Set && pattern::call(tokens::first()::at(:type)) {
      return parser::call(tokens, ...args)
    }
    if pattern is Array && pattern::zip(tokens)::all?(fn([p, token]) {
      let type = token::at(:type)
      if p is Keyword { return p == type }
      if p is Set { return p::has?(type) }
      return false
    }) {
      return parser::call(tokens, ...args)
    }
    if pattern is Keyword && pattern == tokens::first()::at(:type) {
      return parser::call(tokens, ...args)
    }
  }
}

// COIL PARSER START

let math_ops = Set[:mod :plus :minus :times :pow :div]
let comparison_ops = Set[:lt :gt :lt_eq :gt_eq]
let all_math_ops = math_ops::concat(comparison_ops)

fn parse_double_eq(tokens lhs) = Parser[
  Init[{type: :double_equals, lhs}]
  Chomp[:double_eq]
  Then[parse_1_2_expr :rhs]
]::call(tokens)

fn parse_not_eq(tokens lhs) = Parser[
  Init[{type: :not_equals, lhs}]
  Chomp[:not_eq]
  Then[parse_1_2_expr :rhs]
]::call(tokens)

fn parse_triple_eq(tokens lhs) = Parser[
  Init[{type: :triple_equals, lhs}]
  Chomp[:triple_eq]
  Then[parse_1_2_expr :rhs]
]::call(tokens)

fn parse_triple_not_eq(tokens lhs) = Parser[
  Init[{type: :triple_not_equals, lhs}]
  Chomp[:triple_not_eq]
  Then[parse_1_2_expr :rhs]
]::call(tokens)

fn parse_and_and(tokens lhs) = Parser[
  Init[{type: :and_and, lhs}]
  Chomp[:and_and]
  Then[parse_expr :rhs]
]::call(tokens)

fn parse_or_or(tokens lhs) = Parser[
  Init[{type: :or_or, lhs}]
  Chomp[:or_or]
  Then[parse_expr :rhs]
]::call(tokens)

fn parse_nullish(tokens lhs) = Parser[
  Init[{type: :nullish, lhs}]
  Chomp[:nullish]
  Then[parse_expr :rhs]
]::call(tokens)

fn parse_comparison_op(tokens lhs) = Parser[
  Init[{type: :math_op, lhs}]
  Either[comparison_ops :op]
  Then[parse_1_2_expr :rhs]
]::call(tokens)

fn parse_third_expr_step(tokens, lhs) = ParseMap{
  :double_eq => parse_double_eq
  :triple_eq => parse_triple_eq
  :triple_not_eq => parse_triple_not_eq
  :not_eq => parse_not_eq
  :and_and => parse_and_and
  :or_or => parse_or_or
  :nullish => parse_nullish
  comparison_ops => parse_comparison_op
}::call(tokens, lhs)

fn parse_third_expr([lhs tokens]) {  
  while let [new_lhs rest] = parse_third_expr_step(tokens, lhs) {
    lhs = new_lhs
    tokens = rest
  }
  return [lhs tokens]
}

fn parse_partial_obj_dyn_access(tokens) = Parser[
  Init[{type: :partial_obj_dyn_access}]
  Chomp[:open_sq]
  Then[parse_expr :expr]
  Chomp[:close_sq]
]::call(tokens)

fn parse_partial_fn_call(tokens) = Parser[
  Init[{type: :partial_fn_call}]
  Chomp[:open_p]
  Until[:close_p parse_expr :args]
  Chomp[:close_p]
]::call(tokens)

fn parse_and_dot(tokens, lhs) = Parser[  
  Init[{type: :and_dot, lhs}]
  Chomp[:single_and :dot]
  Case[ParseMap{
    :id => parse_id,
    :open_sq => parse_partial_obj_dyn_access
    :open_p => parse_partial_fn_call
  } :rhs]
]::call(tokens)

fn parse_dot(tokens, lhs) = Parser[
  Init[{type: :property_lookup, lhs}]
  Chomp[:dot]
  Either[Set[:id :from] :property]
]::call(tokens)

fn parse_infix_bind(tokens lhs) = Parser[
  Init[{type: :bind, lhs}]
  Chomp[:double_colon]
  Case[ParseMap{
    :id => parse_id
    :fn => parse_fn
    all_math_ops => parse_unapplied_math_op
    :open_p => parse_paren_expr
  } :expr]
]::call(tokens)

fn parse_is(tokens, lhs) = Parser[
  Init[{type: :is, lhs}]
  Chomp[:is]
  Then[parse_1_2_expr, :rhs]
]::call(tokens)

fn parse_snd_assign(tokens, lhs) = Parser[
  Init[{type: :snd_assign, lhs}]
  Chomp[:eq]
  Then[parse_expr :rhs]
]::call(tokens)

fn parse_math_op(tokens lhs) = Parser[
  Init[{type: :math_op, lhs}]
  Either[math_ops :op]
  Then[parse_1_2_expr :rhs]
]::call(tokens)

fn not_adjacent?([_expr tokens]) {
  let current = tokens::first()
  let previous = tokens.collection::at(tokens.idx - 1)
  if current.line != previous.line { return true }
  let end_of_prev_token = previous.col + previous.value.length
  return (current.col - end_of_prev_token) >= 1
}

fn parse_adjacent_1_2_expr(tokens) = Parser[
  AbortIf[not_adjacent?]
  Then[parse_1_2_expr]
]::call(tokens)

fn parse_inclusive_range(tokens lhs) = Parser[
  AbortIf[not_adjacent?]
  Init[{type: :inclusive_range, lhs}]
  Chomp[:dot_dot :eq]
  Optional[SINGLE_EXPR_PARSE_MAP::keys() parse_adjacent_1_2_expr :rhs]
]::call(tokens)

fn parse_exclusive_range(tokens lhs) = Parser[
  AbortIf[not_adjacent?]
  Init[{type: :exclusive_range, lhs}]
  Chomp[:dot_dot]
  Optional[SINGLE_EXPR_PARSE_MAP::keys() parse_adjacent_1_2_expr :rhs]
]::call(tokens)

fn parse_fn_call(tokens lhs) = Parser[
  AbortIf[not_adjacent?]
  Init[{type: :fn_call, lhs}]
  Chomp[:open_p]
  Until[:close_p parse_expr :args]
  Chomp[:close_p]
]::call(tokens)

fn parse_record_lookup(tokens lhs) = Parser[
  AbortIf[not_adjacent?]
  Init[{type: :record_lookup, lhs}]
  Chomp[:open_b]
  Until[:close_b parse_record_entry :entries]
  Chomp[:close_b]
]::call(tokens lhs)

fn parse_dynamic_access(tokens lhs) = Parser[
  AbortIf[not_adjacent?]
  Init[{type: :dynamic_access, lhs}]
  Chomp[:open_sq]
  Until[:close_sq parse_expr :exprs]
  Chomp[:close_sq]
]::call(tokens)

let assignable_ops = math_ops::concat([:or_or, :and_and])

fn parse_op_eq(tokens lhs) = Parser[
  Init[{type: :op_eq, lhs}]  
  Either[assignable_ops :op]
  Chomp[:eq]
  Then[parse_expr :rhs]
]::call(tokens)

fn parse_raw_dynamic_access(tokens lhs) = Parser[
  Init[{type: :raw_dynamic_access, lhs}]
  Chomp[:dot :open_sq]
  Then[parse_expr :expr]
  Chomp[:close_sq]
]::call(tokens)

fn parse_snd_expr_step(tokens, lhs) = ParseMap{
  :open_p => parse_fn_call
  :double_colon => parse_infix_bind
  :open_sq => parse_dynamic_access
  :open_b => parse_record_lookup
  :is => parse_is
  :eq => parse_snd_assign
  [:dot :open_sq] => parse_raw_dynamic_access
  :dot => parse_dot
  [:single_and :dot] => parse_and_dot
  [:dot_dot :eq] => parse_inclusive_range
  :dot_dot => parse_exclusive_range
  [assignable_ops :eq] => parse_op_eq
  math_ops => parse_math_op
}::call(tokens, lhs)

fn parse_snd_expr([lhs tokens]) {  
  while let [new_lhs rest] = parse_snd_expr_step(tokens, lhs) {
    lhs = new_lhs
    tokens = rest
  }
  return [lhs tokens]
}

fn parse_call_expr(tokens) = Parser[
  Chomp[:open_p]
  Until[:close_p parse_expr]
  Chomp[:close_p]
]::call(tokens)

fn parse_decorator_expr(tokens) = ParseMap{
  :id => parse_expr
}::call(tokens)

fn parse_multi_decorator(tokens) = Parser[
  Init[{type: :multi_decorator}]
  Chomp[:at :open_sq]
  Until[:close_sq parse_decorator_expr :decorators]
  Chomp[:close_sq]
  Then[parse_fn :fn_def]
]::call(tokens)

fn parse_decorator(tokens) = Parser[
  Init[{type: :decorator}]
  Chomp[:at]
  One[:id :name]
  Optional[:open_p parse_call_expr :args]
  Then[parse_fn :fn_def]
]::call(tokens)

let parse_regex = Parser[
  Init[{type: :regex_lit}]
  One[:regex_lit :value]
]

let parse_str = Parser[
  Init[{type: :str}]
  One[:string_lit :value]
]

let valid_ids_in_all_contexts = Set[:id :from :as]

let parse_id = Parser[
  Init[{type: :id_lookup}]
  Either[valid_ids_in_all_contexts::push(:import) :name]
]

fn parse_reg_obj_entry(tokens) = Parser[
  Init[{type: :reg_obj_entry}]
  Either[Set[:id :num] :key]
  Chomp[:colon]
  Then[parse_expr :value]
]::call(tokens)

let parse_obj_shorthand_entry = Parser[
  Init[{type: :obj_shorthand_entry}]
  Either[valid_ids_in_all_contexts :id]
]

fn parse_dynamic_obj_entry(tokens) = Parser[
  Init[{type: :dynamic_obj_entry}]
  Chomp[:open_sq]
  Then[parse_expr :key_expr]
  Chomp[:close_sq :colon]
  Then[parse_expr :value]
]::call(tokens)

fn parse_spread_obj_entry(tokens) = Parser[
  Init[{type: :spread_obj_entry}]
  Chomp[:dot_dot_dot]
  Then[parse_expr :expr]
]::call(tokens)

fn parse_obj_entry(tokens) = ParseMap{
  :open_sq => parse_dynamic_obj_entry
  :dot_dot_dot => parse_spread_obj_entry
  :fn => parse_fn
  [:async :fn] => parse_fn
  [:id :colon] => parse_reg_obj_entry
  [:num :colon] => parse_reg_obj_entry
  :id => parse_obj_shorthand_entry
}::call(tokens)

fn parse_obj(tokens) = Parser[
  Init[{type: :obj_lit}]
  Chomp[:open_b]
  Until[:close_b parse_obj_entry :entries]
  Chomp[:close_b]
]::call(tokens)

let parse_spread_assign = Parser[
  Init[{type: :spread_assign}]
  Chomp[:dot_dot_dot]
  Either[valid_ids_in_all_contexts :name]
]

let parse_assign_id = Parser[
  Init[{type: :id_assign}]
  Either[valid_ids_in_all_contexts :name]
]

fn parse_assign_array_entry(tokens) = ParseMap{
  :id => parse_assign_id
  :open_sq => parse_assign_array
  :dot_dot_dot => parse_spread_assign
}::call(tokens)

let parse_assign_array = Parser[
  Init[{type: :array_deconstruction}]
  Chomp[:open_sq]
  Until[:close_sq parse_assign_array_entry :entries]
  Chomp[:close_sq]
]

let parse_obj_entry_rename = Parser[
  Init[{type: :obj_entry_rename}]
  Either[valid_ids_in_all_contexts :old_name]
  Chomp[:colon]
  Either[valid_ids_in_all_contexts :new_name]
]

let parse_regular_obj_assign_entry = Parser[
  Init[{type: :obj_reg_entry}]
  Either[valid_ids_in_all_contexts :name]
]

let parse_string_obj_assign_entry = Parser[
  Init[{type: :obj_str_rename_entry}]
  One[:string_lit :old_name]
  Chomp[:colon]
  Either[valid_ids_in_all_contexts :new_name]
]

let parse_obj_assign_entry = ParseMap{
  [:id :colon] => parse_obj_entry_rename
  :id => parse_regular_obj_assign_entry
  :string_lit => parse_string_obj_assign_entry
  :dot_dot_dot => parse_spread_assign
}

let parse_assign_obj = Parser[
  Init[{type: :object_deconstruction}]
  Chomp[:open_b]
  Until[:close_b parse_obj_assign_entry :entries]
  Chomp[:close_b]
]

let parse_this_assign = Parser[
  Init[{type: :this_assign}]
  Chomp[:at]
  Either[valid_ids_in_all_contexts :name]
]

let parse_assign_expr = ParseMap{
  :id => parse_assign_id
  :open_sq => parse_assign_array
  :open_b => parse_assign_obj
  :dot_dot_dot => parse_spread_assign
  :at => parse_this_assign
}

let parse_keyword = Parser[
  Init[{type: :keyword}]
  One[:keyword :value]
]

fn parse_paren_expr(tokens) = Parser[
  Init[{type: :paren_expr}]
  Chomp[:open_p]
  Then[parse_expr :expr]
  Chomp[:close_p]
]::call(tokens)

fn parse_yield(tokens) = Parser[
  Init[{type: :yield}]
  Chomp[:yield]
  Optional[:times parse_gen_modifier :star?]
  Then[parse_expr :expr]
]::call(tokens)

fn parse_await(tokens) = Parser[
  Init[{type: :await}]
  Chomp[:await]
  Then[parse_expr :expr]
]::call(tokens)

let parse_num = Parser[
  Init[{type: :num}]
  One[:num :value]
]

let parse_custom_number_literal = Parser[
  Init[{type: :custom_number_literal}]
  One[:custom_number_literal :value]
]

fn parse_array(tokens) = Parser[
  Init[{type: :array}]
  Chomp[:open_sq]
  Until[:close_sq parse_expr :elements]
  Chomp[:close_sq]
]::call(tokens)

fn parse_spread(tokens) = Parser[
  Init[{type: :spread}]
  Chomp[:dot_dot_dot]
  Then[parse_expr :expr]
]::call(tokens)

let parse_unapplied_math_op = Parser[
  Init[{type: :unapplied_math_op}]
  Either[all_math_ops :op]
]

fn parse_bind_this(tokens) = Parser[
  Init[{type: :bind_this}]
  Chomp[:double_colon]
  Case[ParseMap{
    :id => parse_id
    :fn => parse_fn
    all_math_ops => parse_unapplied_math_op
    :open_p => parse_paren_expr
  } :expr]
]::call(tokens)

fn parse_not(tokens) = Parser[
  Init[{type: :not}]
  Chomp[:bang]
  Then[parse_1_2_expr :expr]
]::call(tokens)

fn parse_num_raw(tokens) =
  parse_num::call(tokens)::pipe(fn([expr, tokens]) = [expr::at(:value)::as_num(), tokens])

let parse_adjacent_num_raw = Parser[
  AbortIf[not_adjacent?]
  Then[parse_num_raw]
]

let parse_unapplied_and_and = Parser[
  Init[{type: :unapplied_and_and}]
  Chomp[:and_and]
]

let parse_unapplied_or_or = Parser[
  Init[{type: :unapplied_or_or}]
  Chomp[:or_or]
]

let parse_async_modifier = Parser[
  Init[true]
  Chomp[:async]
]

let parse_gen_modifier = Parser[
  Init[true]
  Chomp[:times]
]

fn parse_fn_expr_body(tokens) = Parser[
  Init[{type: :return}]
  Chomp[:eq]
  Then[parse_expr :expr]
  FMap[fn(node) = [node]]
]::call(tokens)

fn parse_args_def(tokens) = Parser[
  Chomp[:open_p]
  Until[:close_p parse_assign_expr]
  Chomp[:close_p]
]::call(tokens)

fn parse_fn_name(tokens) = parse_id::call(tokens)::pipe(fn([expr, tokens]) = [expr::at(:name) tokens])

fn parse_fn(tokens) = Parser[
  Init[{type: :fn}]
  Optional[:async parse_async_modifier :is_async?]
  Chomp[:fn]
  Optional[:times parse_gen_modifier :generator?]
  Optional[:id parse_fn_name :name]
  Optional[:open_p parse_args_def :args]
  Case[ParseMap{
    :eq => parse_fn_expr_body,
    :open_b => block()
  } :body]
]::call(tokens)

fn parse_keyword_record_entry(tokens) = Parser[
  Init[{type: :keyword_record_entry}]
  One[:id :name]
  Chomp[:colon]
  Then[parse_expr :expr]
]::call(tokens)

fn parse_regular_record_entry(tokens) = Parser[
  Init[{type: :regular_record_entry}]
  Then[parse_expr :key_expr]
  Chomp[:arrow]
  Then[parse_expr :value_expr]
]::call(tokens)

fn parse_record_entry(tokens) = ParseMap{
  :dot_dot_dot => parse_spread
  [:id :colon] => parse_keyword_record_entry
  _ => parse_regular_record_entry
}::call(tokens)

fn parse_prefix_exclusive_range(tokens) = Parser[
  Init[{type: :prefix_exclusive_range}]
  Chomp[:dot_dot]
  Then[parse_1_2_expr :expr]
]::call(tokens)

let SINGLE_EXPR_PARSE_MAP = ParseMap{
  :string_lit => parse_str
  :regex_lit => parse_regex
  :keyword => parse_keyword
  :open_p => parse_paren_expr
  :yield => parse_yield
  :await => parse_await
  :num => parse_num
  :custom_number_literal => parse_custom_number_literal
  :open_sq => parse_array
  :dot_dot_dot => parse_spread
  :double_colon => parse_bind_this
  :bang => parse_not
  :open_b => parse_obj
  :and_and => parse_unapplied_and_and
  :or_or => parse_unapplied_or_or
  :dot_dot => parse_prefix_exclusive_range
  valid_ids_in_all_contexts::push(:import) => parse_id
  [:at :open_sq] => parse_multi_decorator
  :at => parse_decorator
  [:async :fn] => parse_fn
  :fn => parse_fn
  all_math_ops => parse_unapplied_math_op
}

fn parse_single_expr(tokens) = SINGLE_EXPR_PARSE_MAP::call(tokens)

fn parse_1_2_expr(tokens) =
  parse_single_expr(tokens)
    ::pipe(parse_snd_expr)

fn parse_expr(tokens) =
  parse_single_expr(tokens)
    ::pipe(parse_snd_expr)
    ::pipe(parse_third_expr)

fn parse_else_branch(tokens) = Parser[
  Init[{type: :else}]  
  Chomp[:else]
  block(:body)
]::call(tokens)

fn parse_else_if_branch(tokens) = Parser[
  Init[{type: :else_if}]  
  Chomp[:else :if]
  Then[parse_expr :expr]
  block(:pass)
  Optional[:else parse_if_branch :fail]
]::call(tokens)

fn parse_if_branch(tokens) = Parser[
  Case[ParseMap{
    [:else :if] => parse_else_if_branch
    :else => parse_else_branch
  }]
]::call(tokens)

fn parse_if(tokens) = Parser[
  Init[{type: :if}]
  Chomp[:if]
  Then[parse_expr :expr]
  block(:pass)
  Optional[:else parse_if_branch :fail]
]::call(tokens)

let parse_let = Parser[
  Init[{type: :let}]
  Chomp[:let]
  Then[parse_assign_expr :assign_expr]
  Chomp[:eq]
  Then[parse_expr :rhs]
]

fn parse_if_let(tokens) = Parser[
  Init[{type: :if_let}]
  Chomp[:if :let]
  Then[parse_assign_expr :assign_expr]
  Chomp[:eq]
  Then[parse_expr :expr]
  Chomp[:open_b]
  Until[:close_b parse_statement :pass]
  Chomp[:close_b]
  Optional[:else parse_else_branch :fail]
]::call(tokens)

let parse_impl_for = Parser[
  Init[{type: :impl_for}]
  Chomp[:impl]
  Then[parse_expr :proto_expr]
  Chomp[:for]
  One[:id :constructor]
  Chomp[:eq]
  Then[parse_expr :expr]
]

let parse_define = Parser[
  Init[{type: :define_for}]
  Chomp[:define]
  Then[parse_expr :proto_expr]
  Chomp[:for]
  Then[parse_single_expr :src_expr]
  Chomp[:eq]
  Then[parse_expr :expr]
]

let parse_protocol = Parser[
  Init[{type: :protocol_def}]
  Chomp[:protocol]
  One[:id :name]
]

let parse_return = Parser[
  Init[{type: :return}]
  Chomp[:return]
  Optional[SINGLE_EXPR_PARSE_MAP::keys() parse_expr :expr]
]

let parse_await_modifier = Parser[
  Init[true]
  Chomp[:await]
]

fn parse_for_loop(tokens) = Parser[
  Init[{type: :for_loop}]
  Chomp[:for]
  Optional[:await parse_await_modifier :is_await?]
  Then[parse_assign_expr :assign_expr]
  Chomp[:of]
  Then[parse_expr :iterable_expr]
  block(:body)
]::call(tokens)

fn parse_loop(tokens) = Parser[
  Init[{type: :loop}]
  Chomp[:loop]
  block(:body)
]::call(tokens)

fn parse_while_loop(tokens) = Parser[
  Init[{type: :while_loop}]
  Chomp[:while]
  Then[parse_expr :test_expr]
  block(:body)
]::call(tokens)

fn parse_while_let_loop(tokens) = Parser[
  Init[{type: :while_let_loop}]
  Chomp[:while :let]
  Then[parse_assign_expr :assign_expr]
  Chomp[:eq]
  Then[parse_expr :test_expr]
  block(:body)
]::call(tokens)

fn parse_continue(tokens) = Parser[
  Init[{type: :continue}]
  Chomp[:continue]
]::call(tokens)

fn parse_break(tokens) = Parser[
  Init[{type: :break}]
  Chomp[:break]
]::call(tokens)

fn parse_catch(tokens) = Parser[
  Init[{type: :catch}]
  Chomp[:catch]
  One[:id :name]
  block(:body)
]::call(tokens)

fn parse_finally(tokens) = Parser[
  Init[{type: :finally}]
  Chomp[:finally]
  block(:body)
]::call(tokens)

fn parse_try(tokens) = Parser[
  Init[{type: :try}]
  Chomp[:try]
  block(:body)
  Optional[:catch parse_catch :catch]
  Optional[:finally parse_finally :finally]
]::call(tokens)

let parse_impl_object = Parser[
  Init[{type: :impl_object}]
  Chomp[:impl]
  One[:id :constructor]
  Chomp[:eq]
  Then[parse_expr :expr]
]

let parse_assign_all_as = Parser[
  Init[{type: :assign_all_as}]
  Chomp[:times :as]
  One[:id :name]
]

fn parse_import_assign_expr(tokens) = Parser[
  Case[ParseMap{
    [:times :as] => parse_assign_all_as
    _ => parse_assign_expr
  }]
]::call(tokens)

fn parse_import(tokens) = Parser[
  Init[{type: :import}]
  Chomp[:import]
  Until[:from parse_import_assign_expr :assign_exprs]
  Chomp[:from]
  Then[parse_str :path]
]::call(tokens)

fn parse_export(tokens) = Parser[
  Init[{type: :export}]
  Chomp[:export]
  Then[parse_statement :statement]
]::call(tokens)

let parse_export_all = Parser[
  Init[{type: :export_all}]
  Chomp[:export :times :from]
  One[:string_lit :path]
]

fn parse_export_default(tokens) = Parser[
  Init[{type: :export_default}]
  Chomp[:export :default]
  Then[parse_expr :expr]
]::call(tokens)

fn parse_label(tokens) = Parser[
  Init[{type: :label}]
  One[:id :label_name]
  Chomp[:colon]
  Then[parse_statement :statement]
]::call(tokens)

fn parse_statement(tokens) = ParseMap{
  :let => parse_let
  :for => parse_for_loop
  :define => parse_define
  :try => parse_try
  :protocol => parse_protocol
  :return => parse_return
  :continue => parse_continue
  :break => parse_break
  :loop => parse_loop
  :import => parse_import
  [:export :default] => parse_export_default
  [:export :times] => parse_export_all
  :export => parse_export
  [:impl :id :eq] => parse_impl_object
  :impl => parse_impl_for
  [:while :let] => parse_while_let_loop
  :while => parse_while_loop
  [:if :let] => parse_if_let
  :if => parse_if
  [:id :colon] => parse_label
  _ => parse_expr
}::call(tokens)

fn block(name) = Parser[
  Chomp[:open_b]
  Until[:close_b parse_statement name]
  Chomp[:close_b]
]

fn parse(tokens) {
  let ast = []
  while let [statement_or_expr, rest] = parse_statement::call(tokens) {
    ast.push(statement_or_expr)
    tokens = rest
  }
  return ast
}

// END OF PARSING

// START OF EVAL

fn map_join(f, separator) =
  this::map(f)::reduce(fn(acc, cur) {
    if acc::empty?() { return cur }
    else { return acc + separator + cur }
  }, "")

fn resolve_name(name) {
  if name {
    return name
      .replaceAll("?", "__q")
      .replaceAll("!", "__b")
      .replaceAll(">", "_lt_")
      .replaceAll("-", "_")
  }
  return name
}

fn eval_if_branch(branch) {
  if branch::nil?() {
    return ""
  } else if branch::at(:type) == :else {
    return str(" else {\n"
      eval_ast(branch::at(:body) || [])
    "\n}")
  } else {
    if branch::at(:type) == :else_if {
      return str(
        " else if (" eval_expr(branch::at(:expr)) ") {\n"
          eval_ast(branch::at(:pass) || [])
        "\n}" eval_if_branch(branch::at(:fail))
      )
    } else {
      raise!(Error["Expected else if"])
    }
  }
}

fn eval_if({expr pass fail}) = str(
  "if (truthy(" eval_expr(expr) ")) {\n"
    eval_ast(pass) "\n"
  "}" eval_if_branch(fail))

fn eval_str({value}) {
  value = value.slice(1, -1)
  if value.includes("\n") {
    return str("`" value "`")
  } else {
    return str("\"" value "\"")
  }
}

let RESERVED_IDS = Set["import"]

fn eval_property_lookup({lhs property}) {
  let lhs_js = eval_expr(lhs)
  if RESERVED_IDS::has?(lhs_js) {
    return str(lhs_js "." property)
  } else {
    return str(lhs_js "['" property "']")
  }
}

fn eval_fn_call({lhs args}) =
  str(eval_expr(lhs) "(" args::map_join(eval_expr, ", ") ")")

fn eval_id_assign_name({name}) = resolve_name(name)

fn eval_spread_assign({name}) = str("..." resolve_name(name))

fn eval_array_deconstruction_entry(node) = node::at(:type)::pipe(Map{
  :id_assign => eval_id_assign_name
  :spread_assign => eval_spread_assign
  :array_deconstruction => eval_array_deconstruction_names
})::call(node)

fn eval_array_deconstruction_names({entries}) =
  str("[" entries::map_join(eval_array_deconstruction_entry ", ") "]")

fn eval_obj_reg_entry({name}) = str("'" name "': " resolve_name(name))

fn eval_obj_entry_rename({old_name, new_name}) = str("'" old_name "': " resolve_name(new_name))

fn eval_obj_deconstruction_entry(node) = node::at(:type)::pipe(Map{
  :obj_reg_entry => eval_obj_reg_entry
  :obj_entry_rename => eval_obj_entry_rename
  :spread_assign => eval_spread_assign
  :obj_str_rename_entry => fn ({old_name new_name}) =
    str(old_name ": " resolve_name(new_name))
})::call(node)

fn eval_object_deconstruction_names({entries}) =
  str("{" entries::map_join(eval_obj_deconstruction_entry ", ") "}")

fn eval_this_assign({name}) = name

fn eval_assign_all_as({name}) =
  str("* as " name)

fn eval_assign_expr(node) = node::at(:type)::pipe(Map{
  :id_assign => eval_id_assign_name
  :spread_assign => eval_spread_assign
  :array_deconstruction => eval_array_deconstruction_names
  :object_deconstruction => eval_object_deconstruction_names
  :this_assign => eval_this_assign
})::call(node)

fn eval_while_let_loop({assign_expr test_expr body}) = str(
  "let __coil_while_let_temp = " eval_expr(test_expr) ";\n"
  "while (__coil_while_let_temp) {\n"
    "let " eval_assign_expr(assign_expr) " = __coil_while_let_temp;\n"
    eval_ast(body) "\n"
    "__coil_while_let_temp = " eval_expr(test_expr) ";\n"
  "}")

fn eval_if_let({assign_expr expr pass fail}) = str(
  "let __coil_if_let_temp = " eval_expr(expr) ";\n"
  "if (truthy(__coil_if_let_temp)) {\n"
  "let " eval_assign_expr(assign_expr) " = __coil_if_let_temp;\n"
    eval_ast(pass) "\n"
  "}" eval_if_branch(fail))

fn eval_spread({expr}) = str("..." eval_expr(expr))

fn eval_let({assign_expr rhs}) =
  str("let " eval_assign_expr(assign_expr) " = " eval_expr(rhs))

fn eval_array({elements}) =
  str("[" elements::map_join(eval_expr ", ") "]")

let math_op_to_method = Map{
  ">" => "greater_than"
  "<" => "less_than"
  ">=" => "greater_than_eq"
  "<=" => "less_than_eq"
  "*" => "times"
  "**" => "exponent"
  "/" => "divide_by"
  "+" => "plus"
  "-" => "minus"
  "%" => "mod"
}

fn eval_math_op({lhs op rhs}) = str(
  math_op_to_method::call(op) ".call("
    eval_expr(lhs) "," eval_expr(rhs)
  ")")

fn eval_this_assignments(args) = args
  ::keep(_::at(:type) == :this_assign)
  ::map(fn({name}) = str("this." name " = " name ";\n"))
  ::into("")

fn eval_fn({is_async? generator? name args body}) = str(
  (is_async? && "async ")
  "function "
    (generator? && "*")
    resolve_name(name)
    "(" args::map_join(eval_assign_expr ", ") ") {\n"
    eval_this_assignments(args)
    eval_ast(body)
  "}")

fn eval_bind({lhs expr}) =
  str(eval_expr(expr) ".bind(" eval_expr(lhs) ")")

fn eval_reg_obj_entry({key, value}) =
  str("'" key "': " eval_expr(value))

fn eval_obj_shorthand_entry({id}) = str("'" id "':" resolve_name(id))

fn eval_dynamic_obj_entry({key_expr value}) =
  str("[" eval_expr(key_expr)"]: " eval_expr(value))

fn eval_obj_fn({name generator? is_async? args body}) =
  str((is_async? && "async ")
      (generator? && "*")
      "['" name "'](" args::map_join(eval_assign_expr ", ") ") {\n"
      eval_ast(body) "\n}")

fn eval_obj_entry(node) = node::at(:type)::pipe(Map{
  :reg_obj_entry => eval_reg_obj_entry
  :obj_shorthand_entry => eval_obj_shorthand_entry
  :dynamic_obj_entry => eval_dynamic_obj_entry
  :spread_obj_entry => eval_spread
  :fn => eval_obj_fn
})::call(node)

fn eval_obj_lit({entries}) =
  str("new ObjectLiteral({"
      entries::map_join(eval_obj_entry ", ") "})")

fn eval_bind_this({expr}) = str(eval_expr(expr) ".bind(this)")
fn eval_id_lookup({name}) = resolve_name(name)
fn eval_num({value}) = str("(" value ")")
fn eval_custom_number_literal({value}) {
  let [num] = value.split(/[a-zA-Z]+/)
  let modifier = value.slice(num.length)
  return str("Keyword.for(\"custom_number_literal/" modifier "\")[CustomNumberLiteral](" num ")")
}
fn eval_double_equals({lhs rhs}) = str(
  resolve_name("equals?") ".call(" eval_expr(lhs) ", " eval_expr(rhs) ")")
fn eval_not_equals(node) = str("negate.call(" eval_double_equals(node) ")")
fn eval_not({expr}) = str("negate.call(" eval_expr(expr) ")")
fn eval_dynamic_access({lhs exprs}) {
  let lhs_js = eval_expr(lhs)
  return str(lhs_js "[Meta]['[]'].call(" lhs_js ", " exprs::map_join(eval_expr ", ") ")")
}
fn eval_record_lookup({lhs entries}) {
  let lhs_js = eval_expr(lhs)
  let entries_js = entries::map_join(eval_record_entry ", ")
  return str(lhs_js "[Meta]['{}'].call(" lhs_js ", [" entries_js "])")
}
fn eval_triple_equals({lhs rhs}) =
  str(eval_expr(lhs) " === " eval_expr(rhs))
fn eval_triple_not_equals({lhs rhs}) =
  str(eval_expr(lhs) " !== " eval_expr(rhs))
fn eval_is({lhs rhs}) =
  str(eval_expr(lhs) " instanceof " eval_expr(rhs))
fn eval_and_and({lhs rhs}) =
  str("and.call(" eval_expr(lhs) ", () => " eval_expr(rhs) ")")
fn eval_or_or({lhs rhs}) =
  str("or.call(" eval_expr(lhs) ", () => " eval_expr(rhs) ")")
fn eval_nullish({lhs, rhs}) = str(eval_expr(lhs) " ?? " eval_expr(rhs))
fn eval_dynamic_access_assign({lhs, rhs}) {
  let {lhs: obj, exprs} = lhs
  let obj_js = eval_expr(obj)
  return str(obj_js "[Meta]['[]='].call(" obj_js ", [" exprs::map_join(eval_expr ", ") "], " eval_expr(rhs) ")")
}
fn eval_general_snd_assign({lhs, rhs}) = str(eval_expr(lhs) " = " eval_expr(rhs))
fn eval_snd_assign(node) = node::pipe(:lhs :type Map{
  :dynamic_access => eval_dynamic_access_assign
  :id_lookup => eval_general_snd_assign
  :property_lookup => eval_general_snd_assign
  :raw_dynamic_access => eval_general_snd_assign
})::call(node)
fn eval_await({expr}) = str("await " eval_expr(expr))
fn eval_yield({star? expr}) =
  str("yield" (star? && "*") " " eval_expr(expr))
fn eval_paren_expr({expr}) = str("(" eval_expr(expr) ")")
fn eval_unapplied_math_op({op}) = math_op_to_method::call(op)
fn eval_unapplied_and_and() = "and"
fn eval_unapplied_or_or() = "or"

fn eval_keyword({value}) = str("Keyword.for(\"" value.slice(1) "\")")

fn eval_regular_record_entry({key_expr value_expr}) =
  str("[" eval_expr(key_expr) ", " eval_expr(value_expr) "]")

fn eval_keyword_record_entry({name expr}) =
  str("[" eval_keyword({value: str(":" name)}) ", " eval_expr(expr) "]")

fn eval_record_entry(node) = node::at(:type)::pipe(Map{
  :regular_record_entry => eval_regular_record_entry
  :keyword_record_entry => eval_keyword_record_entry
  :spread => eval_spread
})::call(node)

fn eval_inclusive_range({lhs, rhs}) {
  if rhs {
    return str("new IRange(" eval_expr(lhs) ", " eval_expr(rhs) ")")
  } else {
    return str("new IRangeNoMax(" eval_expr(lhs) ")")
  }
}

fn eval_exclusive_range({lhs, rhs}) {
  if rhs {
    return str("new ERange(" eval_expr(lhs) ", " eval_expr(rhs) ")")
  } else {
    return str("new ERangeNoMax(" eval_expr(lhs) ")")
  }
}

fn eval_multi_decorator({decorators fn_def}) {
  let fn_name = fn_def::pipe(:name resolve_name)
  let decorator_fn_js = decorators
    ::map(fn (node) {
      if node::pipe(:type Set[:id_lookup]) {
        return node::pipe(:name resolve_name)
      } else {
        let {lhs args} = node
        return str("F => "
          eval_expr(lhs)
          "(F"
          args::len()::pipe(Map{0 => ""} _ || ", ")
          args::map_join(eval_expr ", ") ")")
      }
    })
    ::join(", ")
    ::pipe(fn(decorators) = str("compose(" decorators ")"))

  return str("let " fn_name " = " decorator_fn_js "(" eval_expr(fn_def) ")")
}

fn eval_decorator({name: decorator_name, fn_def args}) {
  let fn_name = fn_def::pipe(:name resolve_name)
  let fn_def_js = eval_fn(fn_def)
  if args::empty?() {
    return str("let " fn_name " = " decorator_name "(" fn_def_js ");")
  } else {
    return str("let " fn_name " = "
      decorator_name "("
        fn_def_js ", "
        args::map_join(eval_expr ", ")
      ");")
  }
}

fn eval_and_dot({lhs rhs}) = str(eval_expr(lhs) "?." eval_expr(rhs))
fn eval_partial_fn_call({expr}) = str("(" eval_expr(expr) ")")
fn eval_partial_obj_dyn_access({expr}) = str("[" eval_expr(expr) "]")
fn eval_regex_lit({value}) = value

let logic_ops = Map{
  "||" => "or",
  "&&" => "and"
}

let all_ops_to_method = math_op_to_method::merge(logic_ops)

fn eval_rhs_based_on_op(op, rhs) {
  if logic_ops::has?(op) {
    return str("() => " eval_expr(rhs))
  } else {
    return eval_expr(rhs)
  }
}

fn eval_op_eq({ lhs, op, rhs }) = 
  str(eval_expr(lhs) " = "
    all_ops_to_method::call(op) ".call("
      eval_expr(lhs) ", "
      eval_rhs_based_on_op(op rhs) ")")

fn eval_prefix_exclusive_range({ expr }) =
  str("new ERangeNoMin(" eval_expr(expr) ")") 

fn eval_raw_dynamic_access({lhs expr}) =
  str(eval_expr(lhs) "[" eval_expr(expr) "]")

fn eval_expr(node) = node::at(:type)::pipe(Map{
  :str => eval_str
  :regex_lit => eval_regex_lit
  :decorator => eval_decorator
  :multi_decorator => eval_multi_decorator
  :keyword => eval_keyword
  :and_dot => eval_and_dot
  :raw_dynamic_access => eval_raw_dynamic_access
  :prefix_exclusive_range => eval_prefix_exclusive_range
  :partial_fn_call => eval_partial_fn_call
  :partial_obj_dyn_access => eval_partial_obj_dyn_access
  :property_lookup => eval_property_lookup
  :id_lookup => eval_id_lookup
  :fn_call => eval_fn_call
  :num => eval_num
  :custom_number_literal => eval_custom_number_literal
  :array => eval_array
  :math_op => eval_math_op
  :double_equals => eval_double_equals
  :not_equals => eval_not_equals
  :not => eval_not
  :fn => eval_fn
  :bind => eval_bind
  :obj_lit => eval_obj_lit
  :bind_this => eval_bind_this
  :dynamic_access => eval_dynamic_access
  :record_lookup => eval_record_lookup
  :triple_equals => eval_triple_equals
  :triple_not_equals => eval_triple_not_equals
  :spread => eval_spread
  :is => eval_is
  :and_and => eval_and_and
  :or_or => eval_or_or
  :nullish => eval_nullish
  :snd_assign => eval_snd_assign
  :await => eval_await
  :yield => eval_yield
  :paren_expr => eval_paren_expr
  :unapplied_math_op => eval_unapplied_math_op
  :unapplied_and_and => eval_unapplied_and_and
  :unapplied_or_or => eval_unapplied_or_or
  :inclusive_range => eval_inclusive_range
  :exclusive_range => eval_exclusive_range
  :op_eq => eval_op_eq
})::call(node)

fn eval_return({expr}) {
  if expr {  
    return str("return " eval_expr(expr))
  } else {
    return "return"
  }
}
fn eval_protocol({name}) = str("const " resolve_name(name) " = Symbol(\"" name "\")")
fn eval_impl_for({proto_expr constructor expr}) =
  str(constructor ".prototype[" eval_expr(proto_expr) "] = " eval_expr(expr))
fn eval_impl_object({constructor expr}) = str(constructor ".prototype = " eval_expr(expr))
fn eval_define_for({proto_expr src_expr expr}) =
  str(eval_expr(src_expr) "[" eval_expr(proto_expr) "] = " eval_expr(expr))

fn eval_for_loop({is_await? assign_expr iterable_expr body}) = 
  str("for "
      (is_await? && "await ")
      " (let " eval_assign_expr(assign_expr) " of "
      eval_expr(iterable_expr) ") {\n"
        eval_ast(body) "\n"
      "}")

fn eval_id_assign({name, expr}) =
  str(resolve_name(name) " = " eval_expr(expr))

fn eval_while_loop({test_expr body}) =
  str("while (" eval_expr(test_expr) ") {\n"
      eval_ast(body) "\n"
      "}")

fn eval_loop({body}) =
  str("while (true) {\n"
      eval_ast(body) "\n"
      "}")

fn eval_continue() = "continue"
fn eval_break() = "break"

fn eval_try(node) {
  let body_js = node::at(:body)::pipe(eval_ast)
  let catch_js = ""
  let finally_js = ""
  if node::has?(:catch) {
    let {name, body} = node::at(:catch)
    catch_js = str(" catch (" name ") {\n" eval_ast(body) "\n}")
  }
  if node::has?(:finally) {
    let {body} = node::at(:finally)
    finally_js = str(" finally {\n" eval_ast(body) "\n}")
  }
  return str("try {\n" body_js "\n" "}" catch_js finally_js)
}

fn get_deconstructed_obj_entry_name(node) =
  Map{:obj_reg_entry => :name
      :obj_entry_rename => :old_name}
    ::at(node::at(:type))
    ::pipe(node)

fn get_deconstructed_array_entry_name(node) =
  Map{:id_assign => :name}
    ::at(node::at(:type))
    ::pipe(node)

fn eval_import_deconstruction_entry(node) = node::pipe(:type Map{
  :obj_reg_entry => _::pipe(:name resolve_name)
  :obj_entry_rename => fn ({old_name new_name}) =
    str(resolve_name(old_name) " as " resolve_name(new_name))
})::call(node)

fn eval_import_deconstruction_expr({entries}) =
  str("{" entries::map_join(eval_import_deconstruction_entry ", ") "}")

fn eval_import_assign_exprs(node) = node::pipe(:type Map{
  :id_assign => eval_id_assign_name
  :object_deconstruction => eval_import_deconstruction_expr
  :assign_all_as => eval_assign_all_as
})::call(node)

fn eval_import({assign_exprs, path}) =
  str("import "
    assign_exprs::map_join(eval_import_assign_exprs, ", ")
    " from \"" path.value.slice(1, -1) "\"")

fn eval_export({statement}) =
  str("export " eval_statement(statement))

fn eval_export_default({expr}) =
  str("export default " eval_expr(expr))

fn eval_export_all({path}) =
  str("export * from " path)

fn eval_label({label_name, statement}) =
  str(label_name ": " eval_statement(statement))

fn eval_statement(node) = node::at(:type)::pipe(Map{
  :label => eval_label  
  :if => eval_if
  :import => eval_import
  :export => eval_export
  :export_default => eval_export_default
  :export_all => eval_export_all
  :let => eval_let
  :if_let => eval_if_let
  :return => eval_return
  :protocol_def => eval_protocol
  :impl_for => eval_impl_for
  :impl_object => eval_impl_object
  :define_for => eval_define_for
  :for_loop => eval_for_loop
  :id_assign => eval_id_assign
  :while_loop => eval_while_loop
  :loop => eval_loop
  :while_let_loop => eval_while_let_loop
  :continue => eval_continue
  :break => eval_break
  :try => eval_try
})
  ::pipe(fn(f) {
    if f { return compose(f _ + ";") }
    else { return eval_expr }
  })::call(node)

export fn eval_ast(ast) = ast::map_join(eval_statement "\n")

export fn lex_and_parse(string) = lexer::call(string)
  ::pipe(fn(tokens) = CollectionView[tokens, 0])
  ::pipe(parse)

export fn compile(string) = lexer::call(string)
  ::pipe(fn(tokens) = CollectionView[tokens, 0])
  ::pipe(parse)
  ::pipe(eval_ast)

fn compile_file(src_file_name, out_name) {
  let src = Deno.readTextFileSync(src_file_name)
  Deno.writeTextFile(out_name, compile(src))
}

fn compile_file_and_prelude(src_file_name, out_name, prelude_src) {
  let prelude = Deno.readTextFileSync("./src/std/js_prelude.js")
  prelude = prelude + compile(Deno.readTextFileSync(prelude_src))
  let src = Deno.readTextFileSync(src_file_name)
  Deno.writeTextFile(out_name, prelude + compile(src))
}

if globalThis.Deno && Deno.args::not_empty?() {
  let src_file_name = Deno.args[0]
  let out_name = Deno.args[1]
  let prelude_src = "./src/std/prelude.coil"

  if Deno.args[2] == "-w" {
    let watcher = Deno.watchFs([src_file_name, prelude_src])
    for await event of watcher {
      if event.kind != "modify" { continue }
      console.log("Rebuilding...")
      try {
        compile_file_and_prelude(src_file_name, out_name, prelude_src)
      } catch e {
        console.error("Compile Failed", e)
      }
    }
  } else if Deno.args[2] == "-no-prelude" {
    compile_file(src_file_name, out_name)
  } else {
    compile_file_and_prelude(src_file_name, out_name, prelude_src)
  } 
}
